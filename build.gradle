buildscript {

    project.ext {
        lombokVersion = "1.18.10"
        mapstructVersion = "1.3.1.Final"
    }

    repositories {
        jcenter()
    }

    dependencies {
        classpath "org.hibernate:hibernate-gradle-plugin:5.4.8.Final"
    }
}

plugins {
    id 'org.springframework.boot' version '2.2.1.RELEASE'
    id 'io.spring.dependency-management' version '1.0.8.RELEASE'
    id 'java'
    id 'idea'
}

// Plugin to enhance compiled Entity
/*apply plugin: 'org.hibernate.orm'
hibernate {
    enhance {
        enableDirtyTracking = true
    }
}*/

wrapper {
    gradleVersion = "6.0.1"
    distributionType = Wrapper.DistributionType.ALL
}

group = 'hibernate.secrets'
version = '1.0.0'

sourceCompatibility = JavaVersion.VERSION_11
targetCompatibility = JavaVersion.VERSION_11

repositories {
    mavenCentral()
}

dependencies {
    // Spring
    implementation 'org.springframework.boot:spring-boot-starter'
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-data-jdbc'
    implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
    implementation 'org.springframework.boot:spring-boot-starter-jdbc'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-devtools'

    // Lombok
    compileOnly "org.projectlombok:lombok:$project.lombokVersion"
    annotationProcessor "org.projectlombok:lombok:$project.lombokVersion"
    testCompileOnly "org.projectlombok:lombok:$project.lombokVersion"
    testAnnotationProcessor "org.projectlombok:lombok:$project.lombokVersion"

    // MapStruct
    implementation "org.mapstruct:mapstruct-jdk8:$project.mapstructVersion"
    compileOnly "org.mapstruct:mapstruct-processor:$project.mapstructVersion"
    annotationProcessor "org.mapstruct:mapstruct-processor:$project.mapstructVersion"

    // DB
    implementation 'p6spy:p6spy:3.8.6'
    runtimeOnly 'com.h2database:h2'

    // Data Faker
    implementation 'com.github.javafaker:javafaker:1.0.1'

    testImplementation('org.springframework.boot:spring-boot-starter-test') {
        exclude group: 'org.junit.vintage', module: 'junit-vintage-engine'
    }
}

test {
    useJUnitPlatform()
}
